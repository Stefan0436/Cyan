plugins {
    id 'java'
	id 'maven-publish'
}
apply from: '../asf.mvn.publish.gradle'
apply from: '../config.gradle'
apply plugin: 'eclipse'
buildDir = '../build/MTK'

group="org.asf.cyan"
def authorname="AerialWorks Software Foundation"

configurePublish {
	pretty_name "Minecraft Toolkit (MTK)"
	description "The Minecraft Toolkit is a set of libraries to help with modding."
	page "https://aerialworks.ddns.net/maven"
	address "https://aerialworks.ddns.net/maven"
	
	author {
		name "$authorname"
		id "ASF"
	}
	
	license {
		name "GNU Lesser General Public License v3.0"
		url "https://www.gnu.org/licenses/lgpl-3.0.txt"
	}
}

sourceCompatibility = '1.9'
targetCompatibility = '1.9'

version="1.0.0.A16"
jar.dependsOn('processResources')

repositories {
    mavenCentral()
    jcenter()
	maven { url = 'https://repo.spongepowered.org/maven' }
	maven { name = "AerialWorks"; url = "https://aerialworks.ddns.net/maven" }
}

dependencies {
	implementation project(':CyanComponents')
	implementation project(':CyanCore')
	implementation project(':CyanUtil')
	implementation project(':CCFG')
	implementation project(':Fluid')
	
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
	implementation 'org.ow2.asm:asm:9.1'
	implementation 'org.ow2.asm:asm-tree:9.1'
	implementation 'org.ow2.asm:asm-commons:9.1'
	
    implementation 'org.javassist:javassist:3.27.1-ASF-SNAPSHOT-5'
	implementation 'org.asf.aos.util.service:aosutil-service-SLIB-UTIL:0.0.0.13'
	implementation group: 'org.jboss.windup.decompiler', name: 'decompiler-fernflower', version: '5.1.2.Final'
	
    testImplementation 'junit:junit:4.12'
}

jar {
	destinationDirectory = file('../build/MTK/Jars')
	manifest {
		attributes([
			"Specification-Title": "Cyan Minecraft Toolkit (MTK)",
			"Specification-Vendor": "${authorname}",
			"Specification-Version": project.version,
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"${authorname}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
	exclude "**/extra/**"
	exclude "**/tests/**"
	includeEmptyDirs = false
}

task javaDocs(type: Javadoc) {
	classpath = sourceSets.main.runtimeClasspath
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javaDocs') {
	from javadoc
	classifier = 'javadoc'
	exclude "**/extra/**"
	destinationDirectory = file('../build/MTK/Javadocs')
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	exclude "**/extra/**"
	destinationDirectory = file('../build/MTK/Source jars')
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

test {
    include "**/**Test**"
    exclude "**/**SpecialTest**"
    minHeapSize = "128m"
  	maxHeapSize = "2G"
}

task specialTest (type: Test, dependsOn: test) {
    include "**/**SpecialTest**"
    minHeapSize = "128m"
  	maxHeapSize = "2G"
}

processResources  {
	inputs.property "version", project.version
	
	from(sourceSets.main.resources.srcDirs) {
		expand "version": project.version
	}
}

initializeASFMVNPublish()
