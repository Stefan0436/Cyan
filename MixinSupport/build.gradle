plugins {
	id 'java'
}
apply from: '../config.gradle'
apply plugin: 'eclipse'
buildDir = '../build/MixinSupport'

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

group="org.asf.cyan"
def author="AerialWorks Software Foundation"

version="1.0.0.A1"
jar.dependsOn('processResources')

repositories {
	mavenCentral()
	maven { url = 'https://repo.spongepowered.org/maven' }
	maven { name = "AerialWorks"; url = "https://aerialworks.ddns.net/maven" }
}

dependencies {
	implementation project(':CyanCore')
	implementation project(':CyanComponents')
	implementation project(':CyanUtil')
	implementation project(':Fluid')

	implementation group: 'org.ow2.asm', name: 'asm', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-util', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-tree', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.3'
	implementation group: 'org.ow2.asm', name: 'asm-analysis', version: '9.3'

	implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'

	implementation "org.spongepowered:mixin:${mixinversion}"
	testImplementation 'junit:junit:4.12'
}

jar {
	destinationDirectory = file('../build/MixinSupport/Jars')
	manifest {
		attributes([
			"Specification-Title": project.name,
			"Specification-Vendor": "${author}",
			"Specification-Version": project.version,
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"${author}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"Export-Package": "org.asf.cyan.common.CyanMixinService"
		])
	}
	exclude "**/extra/**"
	exclude "**/tests/**"
	includeEmptyDirs = false

	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task javaDocs(type: Javadoc) {
	classpath = sourceSets.main.runtimeClasspath
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javaDocs') {
	from javadoc
	classifier = 'javadoc'
	exclude "**/extra/**"
	destinationDirectory = file('../build/MixinSupport/Javadocs')
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	exclude "**/extra/**"
	destinationDirectory = file('../build/MixinSupport/Source jars')
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processResources {
	inputs.property "group", project.group
	inputs.property "minmixin", "${minmixin}"

	from(sourceSets.main.resources.srcDirs) {
		expand(
			"minmixin": "${minmixin}",
			"group": project.group
		)
	}

	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

test {
	include "**/**Test**"
	exclude "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}

task specialTest (type: Test, dependsOn: test) {
	include "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}
