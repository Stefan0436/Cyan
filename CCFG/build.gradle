plugins {
	id 'java'
	id 'maven-publish'
}

apply from: '../asf.mvn.publish.gradle'
apply plugin: 'eclipse'
buildDir = '../build/CCFG'

sourceCompatibility = '1.9'
targetCompatibility = '1.9'

group="org.asf.cyan"
version="1.0.0.A3"

def authorname="AerialWorks Software Foundation"

configurePublish {
	pretty_name "CCFG Serializing Configuration API, Alpha Version 2"
	description "Cyan Configuration API, now with a great big performance patch!"
	page "https://aerialworks.ddns.net/maven"
	address "https://aerialworks.ddns.net/maven"
	
	author {
		name "$authorname"
		id "ASF"
	}
	
	license {
		name "GNU Lesser General Public License v3.0"
		url "https://www.gnu.org/licenses/lgpl-3.0.txt"
	}
}
jar.dependsOn('processResources')

repositories {
	mavenCentral()
	jcenter()
	maven { name = "AerialWorks"; url = "https://aerialworks.ddns.net/maven" }
}

dependencies {
	implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.asf.aos.util.service:aosutil-service-SLIB-UTIL:0.0.0.1'
	testImplementation 'junit:junit:4.12'
}

jar {
	destinationDirectory = file('../build/CCFG/Jars')
	manifest {
		attributes([
			"Specification-Title": project.name,
			"Specification-Vendor": "${authorname}",
			"Specification-Version": project.version,
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"${authorname}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
	exclude "**/extra/**"
	exclude "**/tests/**"
	includeEmptyDirs = false
}

task javaDocs(type: Javadoc) {
	classpath = sourceSets.main.runtimeClasspath
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javaDocs') {
	from javadoc
	classifier = 'javadoc'
	exclude "**/extra/**"
	destinationDirectory = file('../build/CCFG/Javadocs')
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	exclude "**/extra/**"
	destinationDirectory = file('../build/CCFG/Source jars')
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

test {
	include "**/**Test**"
	exclude "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}

task specialTest (type: Test, dependsOn: test) {
	include "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}

initializeASFMVNPublish()
